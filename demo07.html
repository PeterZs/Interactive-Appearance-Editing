<!DOCTYPE html>
<html lang="en">
    <head>
        <title>Demo</title>
        <meta charset="utf-8">
        <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
        <style>
            body {
                color: #000;
                font-family:Monospace;
                font-size:13px;
                text-align:center;
                font-weight: bold;

                background-color: #fff;
                margin: 0px;
                overflow: hidden;
            }

            #info {
                color:#000;
                position: absolute;
                top: 0px; width: 100%;
                padding: 5px;

            }

            a {
                color: red;
            }
        </style>

        <script id="vertexShader" type="x-shader/x-vertex">
            varying vec3 vViewPosition;
            varying vec3 vNormal;

            uniform sampler2D roughnessMap;
            uniform sampler2D diffuseMap;
            varying vec2 vUv;

            void main() {

              vUv = uv;
              vec4 mvPosition = modelViewMatrix * vec4(position, 1.0);
              gl_Position = projectionMatrix * mvPosition;
              vViewPosition = -mvPosition.xyz;
              vNormal = normalMatrix * normal;

            }        
        </script>

        <script id="fragmentShader" type="x-shader/x-fragment">
            varying vec3 vViewPosition;
            varying vec3 vNormal;

            varying vec2 vUv;

            // uniforms
            uniform float metallic;
            uniform vec3 albedo;
            uniform vec3 ambient;
            uniform int diffuseType;

            uniform sampler2D roughnessMap;
            uniform sampler2D diffuseMap;
            uniform sampler2D normalMap;
            uniform sampler2D bumpMap;

            // defines
            #define PI 3.14159265359
            #define PI2 6.28318530718
            #define RECIPROCAL_PI 0.31830988618
            #define RECIPROCAL_PI2 0.15915494
            #define LOG2 1.442695
            #define EPSILON 1e-6

            struct IncidentLight {
              vec3 color;
              vec3 direction;
              bool visible;
            };

            struct ReflectedLight {
              vec3 directDiffuse;
              vec3 directSpecular;
              vec3 indirectDiffuse;
              vec3 indirectSpecular;
            };

            struct GeometricContext {
              vec3 position;
              vec3 normal;
              vec3 viewDir;
            };

            struct Material {
              vec3 diffuseColor;
              float specularRoughness;
              vec3 specularColor;
            };

            // lights
            bool testLightInRange(const in float lightDistance, const in float cutoffDistance) {
              return any(bvec2(cutoffDistance == 0.0, lightDistance < cutoffDistance));
            }

            float punctualLightIntensityToIrradianceFactor(const in float lightDistance, const in float cutoffDistance, const in float decayExponent) {
              if (decayExponent > 0.0) {
                return pow(saturate(-lightDistance / cutoffDistance + 1.0), decayExponent);
              }
              
              return 1.0;
            }

            struct SpotLight {
              vec3 position;
              vec3 direction;
              vec3 color;
              float distance;
              float decay;
              float coneCos;
              float penumbraCos;
            };

            void getSpotDirectLightIrradiance(const in SpotLight spotLight, const in GeometricContext geometry, out IncidentLight directLight) {
              vec3 L = spotLight.position - geometry.position;
              //vec3 L = geometry.position - spotLight.position;
              directLight.direction = normalize(L);
              
              float lightDistance = length(L);
              float angleCos = dot(directLight.direction, spotLight.direction);
              
              if (all(bvec2(angleCos > spotLight.coneCos, testLightInRange(lightDistance, spotLight.distance)))) {
                float spotEffect = smoothstep(spotLight.coneCos, spotLight.penumbraCos, angleCos);
                directLight.color = spotLight.color;
                directLight.color *= spotEffect * punctualLightIntensityToIrradianceFactor(lightDistance, spotLight.distance, spotLight.decay);
                directLight.visible = true;
              } else {
                directLight.color = vec3(0.0);
                directLight.visible = false;
              }
            }

            // light uniforms
            #define LIGHT_MAX 4
            uniform SpotLight spotLights[LIGHT_MAX];
            uniform int numSpotLights;

            // BRDFs

            vec3 GGXOrenNayarDiffuse(vec3 color, float dotNL, float dotNV, float dotNH, float dotLH, float dotLV, float roughness, float a) {
              float f0 = 0.04;
              float theta_i = acos(dotNL);
              float theta_r = acos(dotNV);
              float cos_phi_diff = (dotLV - dotNL*dotNV) / (sin(theta_i)*sin(theta_r) + EPSILON);
              
              float Fr1 = (1.0 - (0.542026*a + 0.303573*roughness) / (a + 1.36053));
              float Fr2 = (1.0 - (pow(1.0 - dotNV, 5.0 - 4.0*a)) / (a + 1.36053));
              float Fr3 = (-0.733996*a*roughness + 1.50912*a - 1.16402*roughness);
              float Fr4 = (pow(1.0 - dotNV, 1.0 + (1.0 / (39.0*a*a + 1.0))));
              float Fr = Fr1*Fr2*(Fr3*Fr4+1.0);
              float Lm1 = (max(1.0 - (2.0*roughness), 0.0)*(1.0 - pow(1.0 - dotNL, 5.0)) + min(2.0*roughness, 1.0)); 
              float Lm2 = ((1.0 - 0.5*roughness)*dotNL + (0.5*roughness)*(pow(dotNL, 2.0)));
                float Lm = Lm1 * Lm2;
                float Vd1 = (a / ((a + 0.09)*(1.31072 + 0.995584*dotNV)));
              float Vd2 = (1.0 - (pow(1.0 - dotNL, (1.0 - 0.3726732*(dotNV*dotNV)) / (0.188566 + 0.38841*dotNV))));
              float Vd = Vd1*Vd2;
              float Bp = dotLV - (dotNV*dotNL);
                if (Bp < 0.0) Bp *= 1.4*dotNV*dotNL;
              float L1 = 1.05*(1.0 - f0)*(Fr*Lm + Vd*Bp);
              return color*(L1*RECIPROCAL_PI);
            }

            vec3 GGXApproxDiffuse(vec3 color, float dotNL, float dotNV, float dotNH, float dotLH, float dotLV, float roughness, float a) {
              float facing = 0.5 + 0.5*dotLV;
              float rough  = facing*(0.9-0.4*facing)*((0.5+dotNH)/dotNH);
              float Smooth = 1.05*(1.0-pow(1.0-dotNL, 5.0)) * (1.0-pow(1.0-dotNV, 5.0));
              float single = mix(Smooth, rough, a) * RECIPROCAL_PI;
              float multi  = 0.1159*a;
              return color*(vec3(single) + color*multi);
            }

            // Normalized Lambert
            vec3 DiffuseBRDF(const in IncidentLight directLight, const in GeometricContext geometry, vec3 diffuseColor, float roughnessFactor) {

              vec3 N = geometry.normal;
              vec3 V = geometry.viewDir;
              vec3 L = directLight.direction;
              
              float dotNL = saturate(dot(N,L));
              float dotNV = saturate(dot(N,V));
              vec3 H = normalize(L+V);
              float dotNH = saturate(dot(N,H));
              float dotVH = saturate(dot(V,H));
              float dotLH = saturate(dot(L,H));
              float dotLV = saturate(dot(L,V));
              float a = roughnessFactor * roughnessFactor;
              
              if (diffuseType >= 9) {
                return GGXApproxDiffuse(diffuseColor, dotNL, dotNV, dotLH, dotVH, dotLV, roughnessFactor, a);
              }
              else if (diffuseType >= 8) {
                return GGXOrenNayarDiffuse(diffuseColor, dotNL, dotNV, dotLH, dotVH, dotLV, roughnessFactor, a);
              }
            }

            vec3 F_Schlick(vec3 specularColor, vec3 H, vec3 V) {
              return (specularColor + (1.0 - specularColor) * pow(1.0 - saturate(dot(V,H)), 5.0));
            }

            float D_GGX(float a, float dotNH) {
              float a2 = a*a;
              float dotNH2 = dotNH*dotNH;
              float d = dotNH2 * (a2 - 1.0) + 1.0;
              return a2 / (PI * d * d);
            }

            float G_Smith_Schlick_GGX(float a, float dotNV, float dotNL) {
              float k = a*a*0.5 + EPSILON;
              float gl = dotNL / (dotNL * (1.0 - k) + k);
              float gv = dotNV / (dotNV * (1.0 - k) + k);
              return gl*gv;
            }

            // Cook-Torrance
            vec3 SpecularBRDF(const in IncidentLight directLight, const in GeometricContext geometry, vec3 specularColor, float roughnessFactor) {
              
              vec3 N = geometry.normal;
              vec3 V = geometry.viewDir;
              vec3 L = directLight.direction;
              
              float dotNL = saturate(dot(N,L));
              float dotNV = saturate(dot(N,V));
              vec3 H = normalize(L+V);
              float dotNH = saturate(dot(N,H));
              float dotVH = saturate(dot(V,H));
              float dotLV = saturate(dot(L,V));
              float a = roughnessFactor * roughnessFactor;

              float D = D_GGX(a, dotNH);
              float G = G_Smith_Schlick_GGX(a, dotNV, dotNL);
              vec3 F = F_Schlick(specularColor, V, H);
              return (F*(G*D))/(4.0*dotNL*dotNV+EPSILON);
            }

            // RenderEquations(RE)
            void RE_Direct(const in IncidentLight directLight, const in GeometricContext geometry, const in Material material, inout ReflectedLight reflectedLight) {
              
              float dotNL = saturate(dot(geometry.normal, directLight.direction));
              vec3 irradiance = dotNL * directLight.color;
              
              // punctual light
              irradiance *= PI;
              
              reflectedLight.directDiffuse += irradiance * DiffuseBRDF(directLight, geometry, material.diffuseColor, material.specularRoughness);

              reflectedLight.directSpecular += irradiance * SpecularBRDF(directLight, geometry, material.specularColor, material.specularRoughness);
            }

            // Convert bump map to normal map

            float textureSize = 1024.0;
            float texelSize =  1.0 / textureSize ; //size of one texel;
            float normalStrength = 20.0;
             
            vec3 ComputeNormals(){

              float tl = abs(texture2D (bumpMap, vUv + texelSize * vec2(-1, -1)).x);   // top left
              float  l = abs(texture2D (bumpMap, vUv + texelSize * vec2(-1,  0)).x);   // left
              float bl = abs(texture2D (bumpMap, vUv + texelSize * vec2(-1,  1)).x);   // bottom left
              float  t = abs(texture2D (bumpMap, vUv + texelSize * vec2( 0, -1)).x);   // top
              float  b = abs(texture2D (bumpMap, vUv + texelSize * vec2( 0,  1)).x);   // bottom
              float tr = abs(texture2D (bumpMap, vUv + texelSize * vec2( 1, -1)).x);   // top right
              float  r = abs(texture2D (bumpMap, vUv + texelSize * vec2( 1,  0)).x);   // right
              float br = abs(texture2D (bumpMap, vUv + texelSize * vec2( 1,  1)).x);   // bottom right


              // Compute dx using Sobel:
              //           -1 0 1 
              //           -2 0 2
              //           -1 0 1

              float dX = tr + 2.0*r + br -tl - 2.0*l - bl;

              // Compute dy using Sobel:
              //           -1 -2 -1 
              //            0  0  0
              //            1  2  1

              float dY = bl + 2.0*b + br -tl - 2.0*t - tr;

              
              dY = 0.1 * dY;
              dX = 0.1 * dX;

              // Build the normalized normal
              vec3 N = normalize(vec3(dX, 1.0 / normalStrength, dY));

              //convert (-1.0 , 1.0) to (0.0 , 1.0), if needed
              return N * 0.5 + 0.5;
            }


            void main() {

              //vec3 ambient = vec3(0.0, 0.0, 0.0);

              GeometricContext geometry;
              geometry.position = -vViewPosition;
              vec3 normal = ComputeNormals();
              normal = normalize(normal * 2.0 - 1.0);
              geometry.normal = normalize(normal);
              geometry.viewDir = normalize(vViewPosition);
              
              Material material;
              vec4 temp_dc = texture2D(diffuseMap, vUv);
              vec3 dc = temp_dc.xyz;
              
              material.diffuseColor = mix(dc, vec3(0.0), metallic);
              material.specularColor = mix(vec3(0.0), dc, metallic);

              vec4 roughnessVector = texture2D(roughnessMap, vUv);
              float roughness = (roughnessVector.x + roughnessVector.y + roughnessVector.z)/3.0;
              material.specularRoughness = roughness/255.0;
              
              // Lighting
              
              ReflectedLight reflectedLight = ReflectedLight(vec3(0.0), vec3(0.0), vec3(0.0), vec3(0.0));
              vec3 emissive = vec3(0.0);
              float opacity = 1.0;
              
              IncidentLight directLight;
              
              // spot light
              for (int i=0; i<LIGHT_MAX; ++i) {
                if (i >= numSpotLights) break;

                getSpotDirectLightIrradiance(spotLights[i], geometry, directLight);

                if (directLight.visible) {
                  RE_Direct(directLight, geometry, material, reflectedLight);
                }
              }


              if (dot(normal, directLight.direction) < 0.0) 
              {
                reflectedLight.directDiffuse = vec3(0.0, 0.0, 0.0); 
              }
              
              
              //vec3 outgoingLight = emissive + reflectedLight.directDiffuse + reflectedLight.directSpecular;

              vec3 outgoingLight = emissive + reflectedLight.directDiffuse + reflectedLight.directSpecular;
              
              gl_FragColor = vec4(outgoingLight, opacity);

            }
        </script>

    </head>

    <body>
        <div id="container"></div>

        <div id="info">
        </div>

        <script src="build/three.js"></script>
        <script src="js/controls/OrbitControls.js"></script>
        <script src="js/Detector.js"></script>
        <script src="js/libs/stats.min.js"></script>
        <script src="js/libs/dat.gui.min.js"></script>
        <script src="js_yuan/loadFiles.js"></script>

        <script>

            if ( ! Detector.webgl ) Detector.addGetWebGLMessage();

            var container = document.getElementById( 'container' );

            var camera, controls, scene, renderer;
            var mouseHelper;
            var line;
            var sphere;
            var stats;

            // texture image
            var imagedata;

            var intersection = {
                intersects: false,
                point: new THREE.Vector3(),
                normal: new THREE.Vector3()
            };
            
            var mouse = new THREE.Vector2();


            initScene();

            animate();



            function initScene() {

                // scene
                scene = new THREE.Scene();
                scene.background = new THREE.Color( 0xcccccc );
                scene.fog = new THREE.FogExp2( 0xcccccc, 0.002 );

                renderer = new THREE.WebGLRenderer( { antialias: true } );
                renderer.setPixelRatio( window.devicePixelRatio );
                renderer.setSize( window.innerWidth, window.innerHeight );
                renderer.shadowMap.enabled = true;
                document.body.appendChild( renderer.domElement );

                stats = new Stats();
                container.appendChild( stats.dom );

                // axes helper
                // show axes in the screen
                var axes = new THREE.AxesHelper(50);
                scene.add(axes);

                // camera
                camera = new THREE.PerspectiveCamera( 60, window.innerWidth / window.innerHeight, 1, 1000 );
                camera.position.set( -15, 15, 15 );
                camera.lookAt(scene.position);

                // controls
                controls = new THREE.OrbitControls( camera, renderer.domElement );

                // texture
                var roughnessMap = new THREE.TextureLoader().load( "assets/textures/brick_roughness.jpg" );
                //roughnessMap.needsUpdate = true; // important
                var diffuseMap = new THREE.TextureLoader().load( "assets/textures/brick_diffuse.jpg" );
                //diffuseMap.needsUpdate = true; // important
                var normalMap = new THREE.TextureLoader().load( "assets/textures/waternormals.jpg" );
                var bumpMap =  new THREE.TextureLoader().load( "assets/textures/brick_bump.jpg" );


                // material
                uniforms = {
                    metallic: {value: 0.0},
                    roughness: {value: 0.0},
                    albedo: {value: new THREE.Color(0xffff00)},
                    roughnessMap: { type: "t", value: roughnessMap },
                    diffuseMap: { type: "t", value: diffuseMap },
                    normalMap: { type: "t", value: normalMap },
                    bumpMap: { type: "t", value: bumpMap },
                    diffuseType: {value:0},
                    pointLights: {value:[]},
                    spotLights: {value:[]},
                    directionalLights: {value:[]},
                    numPointLights: {value: 0},
                    numSpotLights: {value: 0},
                    numDirectionalLights: {value: 0}
                };
                
                for (var i=0; i<4; ++i) {
                    uniforms.pointLights.value.push({
                        position: new THREE.Vector3(0,0,0),
                        color: new THREE.Color(1,1,1),
                        distance: 1.0,
                        decay: 10.0
                    });
                  
                    uniforms.spotLights.value.push({
                        position: new THREE.Vector3(0,0,0),
                        color: new THREE.Color(1,1,1),
                        direction: new THREE.Vector3(0,0,1),
                        distance: 1.0,
                        decay: 10.0,
                        coneCos: 0.0,
                        pnumbraCos: 0.0
                    });
                  
                    uniforms.directionalLights.value.push({
                        direction: new THREE.Vector3(0,0,1),
                        color: new THREE.Color(1,1,1)
                    });
                }
     
                material = new THREE.ShaderMaterial({
                    uniforms: uniforms,
                    vertexShader: document.getElementById( 'vertexShader' ).textContent,
                    fragmentShader: document.getElementById( 'fragmentShader' ).textContent
                });

                // cube geometry
                var boxGeometry = new THREE.BoxGeometry( 10, 10, 10 );
                box = new THREE.Mesh( boxGeometry, material );
                box.position.x = 0;
                box.position.y = 0;
                box.position.z = 0;
                scene.add( box );

                // light
                lights = {};

                lights.spot = new THREE.SpotLight(0xffff00, 100.0, 30.0, 30*THREE.Math.DEG2RAD, 1);
                lights.spot.position.set(-8, 8, 0);
                lights.spotHelper = new THREE.SpotLightHelper(lights.spot, 1.0);
                
                scene.add(lights.spotHelper);

                setupGui();

            }

            function setupGui() {
                    
                effectController = {
                    metallic: uniforms.metallic.value,
                    roughness: uniforms.roughness.value,
                    albedo: 0xff00ff,
                    drawLight: false
                };

                var gui = new dat.GUI();

                //gui.addColor(params, 'color');

                gui.add(effectController, "drawLight");
                //gui.addColor(effectController, 'albedo');
                //gui.add(effectController, "metallic", 0.0, 1.0, 0.01);
                //gui.add(effectController, "roughness", 0.0, 1.0, 0.01);

                gui.open();
            }

            function onWindowResize() {

                camera.aspect = window.innerWidth / window.innerHeight;
                camera.updateProjectionMatrix();

                renderer.setSize( window.innerWidth, window.innerHeight );

            }

            function animate() {

                requestAnimationFrame( animate );

                controls.update(); // only required if controls.enableDamping = true, or if controls.autoRotate = true

                stats.update();

                render();

            }

            function render() {

                stats.update();

                camera.updateMatrixWorld();
                camera.matrixWorldInverse.getInverse(camera.matrixWorld);
                var viewMatrix = camera.matrixWorldInverse;

                uniforms.metallic.value = effectController.metallic;
                uniforms.roughness.value = effectController.roughness;
                uniforms.albedo.value.setHex(effectController.albedo);

                uniforms.numDirectionalLights.value = 0;
                uniforms.numPointLights.value = 0;

                uniforms.numSpotLights.value = 1;
                uniforms.spotLights.value[0].position.copy(lights.spot.position).applyMatrix4(viewMatrix);
                uniforms.spotLights.value[0].direction.copy(lights.spot.position).normalize().transformDirection(viewMatrix);
                uniforms.spotLights.value[0].color.copy(lights.spot.color);
                uniforms.spotLights.value[0].distance = lights.spot.distance;
                uniforms.spotLights.value[0].decay = lights.spot.decay;
                uniforms.spotLights.value[0].coneCos = Math.cos(lights.spot.angle);
                uniforms.spotLights.value[0].penumbraCos = Math.cos(lights.spot.angle * (1.0 - lights.spot.penumbra));

                lights.spotHelper.visible = effectController.drawLight;

                uniforms.diffuseType.value = 8;

                renderer.render(scene, camera);

                scene.overrideMaterial = null;
            }

        </script>

    </body>
</html>
